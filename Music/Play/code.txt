$varExistError[bdfd-lavalink;Missing Variable : ` bdfd-lavalink `]

$onlyIf[$guildID!=;You can't use this command in DM channels.]

$if[$channelType[$channelID]==thread]
    $onlyBotPerms[sendmessagesinthreads;Missing Permission : ` SEND_MESSAGES_IN_THREADS `]
$else
     $onlyBotPerms[sendmessages;Missing Permission : ` SEND_MESSAGES `]
$endif

$onlyBotPerms[embedlinks;Missing Permission : ` EMBED_LINKS `]
$onlyBotPerms[connect;Missing Permission : ` CONNECT `]
$onlyBotPerms[speak;Missing Permission : ` SPEAK `]

$argsCheck[>1;Type a song name/put a YouTube link to play!]

$try
    $httpGet[$getVar[bdfd-lavalink]/voice_channel_id/$guildID/$botID]
    $var[BotVC;$httpResult[voice_id]]

    $httpGet[$getVar[bdfd-lavalink]/voice_channel_id/$guildID/$authorID]
    $var[AuthorVC;$optOff[$httpResult[voice_id]]]
$catch
      $onlyIf[1==2;Failed to get response from bdfd-lavalink API!]
$endtry

$onlyIf[$var[AuthorVC]!=;You aren't connected to any voice channels.]

$if[$var[BotVC]!=]
    $onlyIf[$var[AuthorVC]==$var[BotVC];You aren't connected in same voice channel.]
$else
     $httpPost[$getVar[bdfd-lavalink]/join_voice_channel;{"id":"$var[AuthorVC]"}]

     $var[Type;$httpResult[type]]

     $onlyIf[$var[Type]!=UNKNOWN_CHANNEL;Error! Cannot identify channel as a voice type.]
     $onlyIf[$var[Type]!=NOT_JOINABLE;Error! Cannot hop into this voice channel.]
     $onlyIf[$var[Type]!=MISSING_PERMISSIONS;Missing Permission : ` CONNECT `]
$endif

$if[$checkContains[$message;https://]]
    $var[Query;$message]
$else
     $var[Query;$url[encode;$message]]
$endif

$httpGet[$getVar[bdfd-lavalink]/search?q=$var[Query]]

$onlyIf[$httpResult[tracks;0;url]!=;Error! Cannot find the requested YouTube video.]

$var[IsStream;$httpResult[tracks;0;isStream]]
$var[Embed;$title[$httpResult[tracks;0;title]]
$embeddedURL[$httpResult[tracks;0;url]]
$thumbnail[$replaceText[$httpResult[tracks;0;thumbnail];2048;maxresdefault]]
$footer[Author : $httpResult[tracks;0;author]
Duration : $replaceText[$replaceText[$checkCondition[$var[IsStream]==true];true;Live];false;$httpResult[tracks;0;relative]]
Requested by : $username#$discriminator[]]]

$httpPost[$getVar[bdfd-lavalink]/search_and_play/$guildID?q=$httpResult[tracks;0;url];{}]

$var[Type;$optOff[$httpResult[type]]]

$onlyIf[$var[Type]!=UNKNOWN_MEMBER;Error! Unknown Member.]
$onlyIf[$var[Type]!=NO_VOICE_CHANNEL;Error! Bot ain't connected to voice channel.]
$onlyIf[$var[Type]!=FAILED;Error! Failed to play the requested track.]

$nomention
$author[$if[$httpResult[playing_now]]$if[$var[IsStream]]Streaming Now!$elsePlaying Now!$endif$elseAdded to Queue!$endif]
$unescape[$var[Embed]]
$color[$random[1;16777216]]
