$varExistError[bdfd-lavalink;Missing Variable : ` bdfd-lavalink `]

$onlyIf[$guildID!=;You can't use this command in DM channels.]

$if[$channelType[$channelID]==thread]
    $onlyBotPerms[sendmessagesinthreads;Missing Permission : ` SEND_MESSAGES_IN_THREADS `]
$else
     $onlyBotPerms[sendmessages;Missing Permission : ` SEND_MESSAGES `]
$endif

$onlyBotPerms[embedlinks;Missing Permission : ` EMBED_LINKS `]
$onlyBotPerms[connect;Missing Permission : ` CONNECT `]
$onlyBotPerms[speak;Missing Permission : ` SPEAK `]

$argsCheck[>1;Type a song name/put a YouTube link to play!]

$try
    $httpGet[$getVar[bdfd-lavalink]/voice_channel_id/$guildID/$botID]
    $var[BotVC;$httpResult[voice_id]]

    $httpGet[$getVar[bdfd-lavalink]/voice_channel_id/$guildID/$authorID]
    $var[AuthorVC;$optOff[$httpResult[voice_id]]]
$catch
      $onlyIf[1==2;Failed to get response from bdfd-lavalink API!]
$endtry

$onlyIf[$var[AuthorVC]!=;You aren't connected to any voice channels.]

$if[$var[BotVC]!=]
    $onlyIf[$var[AuthorVC]==$var[BotVC];You aren't connected in same voice channel.]
$else
     $httpPost[$getVar[Music]/join_voice_channel;{"id":"$var[AuthorVC]"}]

     $var[Type;$httpResult[type]]

     $onlyIf[$var[Type]!=UNKNOWN_CHANNEL;Error! Cannot identify channel as a voice type.]
     $onlyIf[$var[Type]!=NOT_JOINABLE;Error! Cannot hop into this voice channel.]
     $onlyIf[$var[Type]!=MISSING_PERMISSIONS;Missing Permission : ` CONNECT `]
$endif

$if[$checkContains[$message;https://]]
    $var[Query;$message]
$else
     $var[Query;$url[encode;$message]]
$endif

$httpGet[$getVar[bdfd-lavalink]/search?q=$var[Query]]

$onlyIf[$httpResult[tracks;0;url]!=;Error! Cannot find the requested YouTube video.]

$var[IsStream;$httpResult[tracks;0;isStream]]

$if[$var[IsStream]]
    $var[Length;Live]
$else
     $var[Duration;$round[$httpResult[tracks;0;duration]]]

     $var[TH;$calculate[((($var[Duration]/1000)/60)/60)]]
     $var[TS;$calculate[($var[Duration]/1000)-((($var[Duration]/1000)/60)*60)]]
     $var[TM;$calculate[(($var[Duration]/1000)/60)-(((($var[Duration]/1000)/60)/60)*60)]]

     $if[$var[TH]>0]
         $var[H;$var[TH]h]
     $endif

     $if[$var[TM]>0]
         $var[M;$var[TM]m]
     $endif

     $if[$var[TS]>0]
         $var[S;$var[TS]s]
     $endif

     $var[Length;$var[H] $var[M] $var[S]]
$endif

$nomention

$var[Embed;$title[$httpResult[tracks;0;title]]
$embeddedURL[$httpResult[tracks;0;url]]
$thumbnail[$replaceText[$httpResult[tracks;0;thumbnail];2048;maxresdefault]]
$footer[Author : $httpResult[tracks;0;author]
Duration : $var[Length]
Requested by : $username#$discriminator[]]]

$httpPost[$getVar[Music]/search_and_play/$guildID?q=$httpResult[tracks;0;url];{}]

$var[Type;$optOff[$httpResult[type]]]

$onlyIf[$var[Type]!=UNKNOWN_GUILD;Error! Unknown Guild.]
$onlyIf[$var[Type]!=UNKNOWN_MEMBER;Error! Unknown Member.]
$onlyIf[$var[Type]!=NO_VOICE_CHANNEL;Error! Bot ain't connected to voice channel.]
$onlyIf[$var[Type]!=FAILED;Error! Failed to play the requested track.]

$author[$if[$httpResult[playing_now]]$if[$var[IsStream]]Streaming Now!$elsePlaying Now!$endif$elseAdded to Queue!$endif]
$unescape[$var[Embed]]
$color[$random[1;16777216]]
