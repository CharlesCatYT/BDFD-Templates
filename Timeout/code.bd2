$c[Permission check!]
$onlyPerms[moderatemembers;:x: Missing `Moderate_Members` permission.]
$onlyBotPerms[moderatemembers;:x: I am missing `Moderate_Members` permission.]

$c[Argument check!]
$argsCheck[>1;:x: Missing `{user}` and `{duration}` arguments!]
$argsCheck[>2;:x: Missing `{duration}` argument!]

$c[Conversion of last argument into lowercase!]
$var[Lowercase;$toLowercase[$message[>]]]

$c[Last argument character check & count!]
$onlyIf[$checkContains[$var[Lowercase];s;d;m;h;cancel]==true;You either forgot to put unit in `{duration}` argument or typed an invalid character.
> Supported characters are **s**, **m**, **h**, **d** and **cancel**.]

$c[Storing user id in temp var!]
$var[User;$findUser[$replaceText[$message;$message[>];;1];no]]

$c[Embed]
$nomention
$author[$username[$var[User]]#$discriminator[$var[User]]]
$footer[Follow github.com/BDFD-Templates for more!]

$c[Limiter v1]
$onlyIf[$var[User]!=$authorID;:x: You can't timeout/untimeout yourself.]
$onlyIf[$var[User]!=$serverOwner;:x: Timeout/untimeout is not supported towards server owner.]
$onlyIf[$var[User]!=$botID;:x: Timeout/untimeout is not supported towards me.]
$onlyIf[$rolePosition[$highestRole[$var[User]]]>$rolePosition[$highestRole[$authorID]];:x: You can't timeout/untimeout members having roles higher than you.]
$onlyIf[$rolePosition[$highestRole[$var[User]]]>$rolePosition[$highestRole[$botID]];:x: I can't timeout/untimeout members having roles higher than me.]

$c[Untimeout Embed]
$if[$checkContains[$var[Lowercase];cancel]==true]
    $description[has been **Untimeout** by <@$authorID>]
    $untimeout[$var[User]]
$else
     $onlyIf[$charCount[$message[>]]<4;:x: Max character limit in `{duration}` argument is `3`.]

$c[Storing duration in temp var!]
$var[Duration;$replaceText[$replaceText[$replaceText[$replaceText[$var[Lowercase];s;;1];m;;1];d;;1];h;;1]]

$c[Limiters v2]
$onlyIf[$isNumber[$var[Duration]]==true;:x: Invalid value in `{duration}`.]

$if[$checkContains[$var[Lowercase];d]==true]
   $var[28;$replaceText[$var[Lowercase];d;;1]]
   $onlyIf[$var[28]<29;:x: Max timeout limit is `28` days.]
$elseif[$checkContains[$var[Lowercase];m]==true]
        $var[60;$replaceText[$var[Lowercase];m;;1]]
        $onlyIf[$var[60]<61;:x: Max minutes is `60`]
$elseif[$checkContains[$var[Lowercase];s]==true]
        $var[60;$replaceText[$var[Lowercase];s;;1]]
        $onlyIf[$var[60]<61;:x: Max seconds is `60`]
$elseif[$checkContains[$var[Lowercase];h]==true]
        $var[24;$replaceText[$var[Lowercase];h;;1]]
        $onlyIf[$var[24]<25;:x: Max hours is `24`]
$endif

$c[Conversion of plurality]
$if[$checkContains[$var[Lowercase];s]==true]
   $if[$var[Duration]==1]
      $var[Unit;second]
   $else
        $var[Unit;seconds]
   $endif
$elseif[$checkContains[$var[Lowercase];d]==true]
        $if[$var[Duration]==1]
           $var[Unit;day]
        $else
             $var[Unit;days]
        $endif
$elseif[$checkContains[$var[Lowercase];m]==true]
        $if[$var[Duration]==1]
            $var[Unit;minute]
        $else
             $var[Unit;minutes]
        $endif
$elseif[$checkContains[$var[Lowercase];h]==true]
        $if[$var[Duration]==1]
            $var[Unit;hour]
        $else
             $var[Unit;hours]
        $endif
$endif

$c[Storing duration & unit in one temp variable]
$var[Time;$var[Duration] $var[Unit]]

$c[Time formatting]
$if[$var[Time]==60 seconds]
$var[Time;1 minute]
$elseif[$var[Time]==60 minutes]
$var[Time;1 hour]
$elseif[$var[Time]==24 hours]
$var[Time;1 day]
$endif

$c[Timeout embed]
$description[has been **Timeout** for $var[Time] by <@$authorID>]
$timeout[$var[Duration];$var[User]]
$endif
